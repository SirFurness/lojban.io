{-# LANGUAGE OverloadedStrings #-}

module Language.Lojban.Refinement
( simplifyTerminatorsInSentence
, simplifyTerminatorsInBridiDisplayer
) where

import Language.Lojban.Core
import Language.Lojban.Canonicalization (basicSentenceCanonicalizer)
import Control.Arrow ((***))
import System.Random (StdGen)
import Util (compose2)
import qualified Data.Text as T

-- * Terminator simplification
-- | Replaces terminators with "cu" whenever possible
replaceElidableTerminatorsInSentence :: T.Text -> T.Text
replaceElidableTerminatorsInSentence t = f [] (T.words t) where
    originalCanonicalization = basicSentenceCanonicalizer t
    f :: [T.Text] -> [T.Text] -> T.Text
    f x [] = T.unwords x
    f x (y:ys) = if basicSentenceCanonicalizer (T.unwords $ x++("cu":ys)) == originalCanonicalization then f (x++["cu"]) ys else f (x++[y]) ys

-- | Removes redundant elidable terminators ("ku", "kei", etc)
removeElidableTerminatorsInSentence :: T.Text -> T.Text
removeElidableTerminatorsInSentence t = f [] (T.words t) where
    originalCanonicalization = basicSentenceCanonicalizer t
    f :: [T.Text] -> [T.Text] -> T.Text
    f x [] = T.unwords x
    f x (y:ys) = if basicSentenceCanonicalizer (T.unwords $ x++ys) == originalCanonicalization then f x ys else f (x++[y]) ys

-- | Simplifies sentence by removing elidable terminators and/or replacing them with "cu"
simplifyTerminatorsInSentence :: T.Text -> T.Text
simplifyTerminatorsInSentence = removeElidableTerminatorsInSentence . replaceElidableTerminatorsInSentence

-- | Simplifies the resulting bridi generated by the displayer by removing elidable terminators and/or replacing them with "cu"
simplifyTerminatorsInBridiDisplayer :: SimpleBridiDisplayer -> SimpleBridiDisplayer
simplifyTerminatorsInBridiDisplayer bridiDisplayer = simplifySentence `compose2` bridiDisplayer where
    simplifySentence :: (T.Text, StdGen) -> (T.Text, StdGen)
    simplifySentence = simplifyTerminatorsInSentence *** id
